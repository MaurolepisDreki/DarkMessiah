# CMake Uninstall Routine

## Generic Unistall
if( NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" )
	message( FATAL_ERROR "Cannot find install manifest: \"@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt\"" )
endif( NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" )

file( READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" files )
string( REGEX REPLACE "\n" ";" files "${files}" )
foreach( file ${files} )
	message( STATUS "Uninstalling: $ENV{DESTDIR}${file}" )
	if( EXISTS "$ENV{DESTDIR}${file}" )
		execute_process(
			COMMAND "@RM@" --interactive=never "$ENV{DESTDIR}${file}"
			RESULT_VARIABLE rm_retval
			ERROR_VARIABLE rm_error
		)

		if( NOT "${rm_retval}" STREQUAL 0 )
			message( FATAL_ERROR "${rm_error}" )
		endif( NOT "${rm_retval}" STREQUAL 0 )
	else( EXISTS "$ENV{DESTDIR}${file}" )
		message( NOTICE "File `$ENV{DESTDIR}${file}' does not exist." )
	endif( EXISTS "$ENV{DESTDIR}${file}" )
endforeach( file )

## Specific Uninstall
# Remove Leftover Directories
foreach( dir IN ITEMS "@DM_INSTALL_BIN@" "@DM_INSTALL_LIB@" "@DM_INSTALL_DIR@" )
	message( STATUS "Pruning: $ENV{DESTDIR}${dir}" )
	if( EXISTS "$ENV{DESTDIR}${dir}" )
		execute_process(
			COMMAND "@RM@" --interactive=never --dir "$ENV{DESTDIR}${dir}"
			RESULT_VARIABLE rm_retval
			ERROR_VARIABLE rm_error
		)

		if( NOT "${rm_retval}" STREQUAL 0 )
			message( WARNING "${rm_error}" )
		endif( NOT "${rm_retval}" STREQUAL 0 )
	else( EXISTS "$ENV{DESTDIR}${dir}" )
		message( NOTICE "`$ENV{DESTDIR}${dir}' does not exist." )
	endif( EXISTS "$ENV{DESTDIR}${dir}" )
endforeach( dir )

